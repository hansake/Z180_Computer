Name            Z180_ADR;
Partno          U5;
Revision        1.0;
Date            2021-07-18;
Designer        hal;
Company         atHome;
Location        None;
Assembly        None;
Device          G22V10;

/*
 * Inputs: CPU clock, memory and io access signal and addresses
 */
Pin 1 = CLK;
Pin 2 = MREQ;
Pin 3 = IORQ;
Pin 4 = RD;
Pin 5 = WR;
Pin 6 = A3;
Pin 7 = A4;
Pin 8 = A5;
Pin 9 = A6;
Pin 10 = A7;
Pin 11 = A18;
Pin 13 = A19;

/* Outputs: select register, i/o and memory select
 * 
 */
Pin 23 = CLKREG;
Pin 22 = ROMCE;
Pin 21 = RAMLCE;
Pin 20 = RAMHCE;
Pin 19 = MEMSEL;
Pin 14 = LED;


/*
 * Logic to control i/o addresses and memory
 */

/* Decode I/O addresses to select EPROM or RAM in lower memory */

ROMSEL = !IORQ & !WR & !A3 & !A4 & !A5 & A6 & !A7; /* Write to 0x40 - 0x47 selects EPROM */
RAMSEL = !IORQ & !WR & A3 & !A4 & !A5 & A6 & !A7; /* Write to 0x48 - 0x4f selects RAM */

MEMSEL.D = (MEMSEL & !ROMSEL)  # RAMSEL;
MEMSEL.SP = 'b'0;
MEMSEL.AR = 'b'0;

/* Latch data to 74LS74 based selection register */

CLKREG = CLK & !IORQ & !WR & !A3 & A4 & !A5 & A6 & !A7; /* Write to 0x50 - 0x57 latches data */

/* Decode I/O addresses to control LED on/off */

LEDOFF = !IORQ & !WR & !A3 & !A4 & A5 & A6 & !A7; /* Write to 0x60 - 0x67 turns LED off */
LEDON = !IORQ & !WR & A3 & !A4 & A5 & A6 & !A7; /* Write to 0x68 - 0x6f turns LED on */

LED.D = (LED & !LEDOFF)  # LEDON;
LED.SP = 'b'0;
LED.AR = 'b'0;

/* Decode memory addresses */

 /* High 512K RAM */
RAMHCE = !(!MREQ & A19);

/* Low 512K all RAM if no EPROM is selected (MEMSEL == 'b'1') */
/* RAM between 256K and 512K if EPROM is selected (MEMSEL == 'b'0') */
RAMLCE = !(!MREQ & ((!A19 & MEMSEL) # (A18 & !A19 & !MEMSEL)));

 /* Low 256K EPROM if selected (MEMSEL == 'b'0'') */
ROMCE = !(!MREQ & !A18 & !A19 & !MEMSEL);
